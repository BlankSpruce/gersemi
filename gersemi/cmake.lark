start: file

file          : (_file_element NEWLINE)* _file_element
_file_element : command_element
              | non_command_element

command_element     : leading_space command_invocation trailing_space [line_comment]
leading_space       : [SPACE]
trailing_space      : [SPACE]
non_command_element : (bracket_comment | SPACE)* [line_comment]

command_invocation  : identifier [SPACE] _LEFT_PARENTHESIS arguments _RIGHT_PARENTHESIS
identifier          : /[A-Za-z_][A-Za-z0-9_]*/
arguments           : [argument] (_separation [argument])*
_separation         : _separation_atom+
_separation_atom    : SPACE
                    | bracket_comment
                    | [line_comment] NEWLINE

argument : bracket_argument
         | quoted_argument
         | unquoted_argument
         | complex_argument

bracket_argument : BRACKET_ARGUMENT
BRACKET_ARGUMENT : /\[(?P<equal_signs>(=*))\[([\s\S]+?)\](?P=equal_signs)\]/

quoted_argument     : _QUOTATION_MARK quoted_element* _QUOTATION_MARK
quoted_element      : /([^\\\"]|\n)/
                    | BACKSLASH ESCAPE_SEQUENCE_CHARACTER -> escape_sequence
                    | quoted_continuation
quoted_continuation : BACKSLASH NEWLINE

unquoted_argument : unquoted_element+
unquoted_element  : /[^\s\(\)#\"\\]/
                  | BACKSLASH ESCAPE_SEQUENCE_CHARACTER -> escape_sequence
                  | make_style_reference
                  | unquoted_element double_quoted_string

make_style_reference : /\$\([^\)\n\"#]+?\)/
double_quoted_string : /\"[^\n#]*?\"/

complex_argument : _LEFT_PARENTHESIS arguments _RIGHT_PARENTHESIS

ESCAPE_SEQUENCE_CHARACTER : /[^A-Za-z0-9]|[nrt]/

bracket_comment : _POUND_SIGN bracket_argument
line_comment    : _POUND_SIGN [LINE_COMMENT_CONTENT]
LINE_COMMENT_CONTENT : /.+/

BACKSLASH              : "\\"
_LEFT_PARENTHESIS      : /\(/
NEWLINE                : "\n"
_POUND_SIGN            : "#"
_QUOTATION_MARK        : "\""
_RIGHT_PARENTHESIS     : /\)/
SPACE                  : /[ \t]+/
