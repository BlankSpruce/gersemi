start: file

file         : file_element*
file_element : command_invocation [SPACE] line_ending
             | (bracket_comment | SPACE)* line_ending
line_ending  : [line_comment] NEWLINE

command_invocation  : [SPACE] IDENTIFIER [SPACE] LEFT_PARENTHESIS arguments RIGHT_PARENTHESIS
IDENTIFIER          : /[A-Za-z_][A-Za-z0-9_]*/
arguments           : [argument] separated_arguments*
separated_arguments : separation+ [argument]
                    | separation* LEFT_PARENTHESIS arguments RIGHT_PARENTHESIS
separation          : SPACE
                    | bracket_comment
                    | line_ending

argument : bracket_argument
         | quoted_argument
         | unquoted_argument

bracket_argument      : LEFT_BRACKET inside_outer_brackets RIGHT_BRACKET
inside_outer_brackets : BRACKET_CONTENT
                      | EQUAL_SIGN inside_outer_brackets EQUAL_SIGN
BRACKET_CONTENT       : /\[(.|\n|\r)+?\]/
                      | /=\[(.|\n|\r)+?\]=/

quoted_argument     : QUOTATION_MARK quoted_element* QUOTATION_MARK
quoted_element      : /([^\\\"]|\n)/
                    | BACKSLASH ESCAPE_SEQUENCE_CHARACTER -> escape_sequence
                    | quoted_continuation
quoted_continuation : BACKSLASH NEWLINE

unquoted_argument : unquoted_element+ // TODO: unquoted_legacy
unquoted_element  : /(?!\[=*\[)[^\s\(\)#\"\\]/
                  | BACKSLASH ESCAPE_SEQUENCE_CHARACTER -> escape_sequence

ESCAPE_SEQUENCE_CHARACTER : /[^A-Za-z0-9]|[nrt]/

bracket_comment : POUND_SIGN bracket_argument
line_comment    : POUND_SIGN LINE_COMMENT_CONTENT
LINE_COMMENT_CONTENT : /(?!\[=*\[).+/

BACKSLASH              : "\\"
EQUAL_SIGN             : "="
LEFT_BRACKET           : /\[/
LEFT_PARENTHESIS       : /\(/
NEWLINE                : "\n"
POUND_SIGN             : "#"
QUOTATION_MARK         : "\""
RIGHT_BRACKET          : /\]/
RIGHT_PARENTHESIS      : /\)/
SPACE                  : /[ \t]+/
