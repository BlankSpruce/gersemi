start: file

file         : (file_element NEWLINE)* file_element
file_element : command_invocation [SPACE] [line_comment]
             | (bracket_comment | SPACE)* [line_comment]

command_invocation  : [SPACE] IDENTIFIER [SPACE] LEFT_PARENTHESIS arguments RIGHT_PARENTHESIS
IDENTIFIER          : /[A-Za-z_][A-Za-z0-9_]*/
arguments           : [argument] separated_arguments*
separated_arguments : separation+ [argument]
                    | separation* LEFT_PARENTHESIS arguments RIGHT_PARENTHESIS
separation          : SPACE
                    | bracket_comment
                    | [line_comment] NEWLINE -> line_ending

argument : bracket_argument
         | quoted_argument
         | unquoted_argument

bracket_argument : /\[(?P<equal_signs>(=*))\[(.|\n|\r)+?\](?P=equal_signs)\]/

quoted_argument     : QUOTATION_MARK quoted_element* QUOTATION_MARK
quoted_element      : /([^\\\"]|\n)/
                    | BACKSLASH ESCAPE_SEQUENCE_CHARACTER -> escape_sequence
                    | quoted_continuation
quoted_continuation : BACKSLASH NEWLINE

unquoted_argument : unquoted_element+
unquoted_element  : /[^\s\(\)#\"\\]/
                  | BACKSLASH ESCAPE_SEQUENCE_CHARACTER -> escape_sequence
                  | make_style_reference
                  | unquoted_element double_quoted_string

make_style_reference : /\$\([^\)\n\"#]+?\)/
double_quoted_string : /\"[^\n#]*?\"/

ESCAPE_SEQUENCE_CHARACTER : /[^A-Za-z0-9]|[nrt]/

bracket_comment : POUND_SIGN bracket_argument
line_comment    : POUND_SIGN [LINE_COMMENT_CONTENT]
LINE_COMMENT_CONTENT : /.+/

BACKSLASH              : "\\"
LEFT_PARENTHESIS       : /\(/
NEWLINE                : "\n"
POUND_SIGN             : "#"
QUOTATION_MARK         : "\""
RIGHT_PARENTHESIS      : /\)/
SPACE                  : /[ \t]+/
