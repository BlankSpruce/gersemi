start: file

file         : file_element*
file_element : command_invocation line_ending
             | (bracket_comment | SPACE)* line_ending
line_ending  : [line_comment] newline
SPACE        : /[ \t]+/
newline      : NEWLINE

command_invocation  : SPACE* identifier SPACE* LEFT_PARENTHESIS arguments RIGHT_PARENTHESIS
identifier          : /[A-Za-z_][A-Za-z0-9_]*/
arguments           : argument? separated_arguments*
separated_arguments : separation+ argument?
                    | separation* LEFT_PARENTHESIS arguments RIGHT_PARENTHESIS
separation          : SPACE
                    | line_ending

argument : bracket_argument
         | quoted_argument
         | unquoted_argument

bracket_argument : bracket_open bracket_content bracket_close
bracket_open     : "[" "="* "["
bracket_content  : /.+/
bracket_close    : "]" "="* "]"

quoted_argument     : QUOTATION_MARK quoted_element* QUOTATION_MARK
quoted_element      : /[^\\\"]/
                    | escape_sequence
                    | quoted_continuation
quoted_continuation : "\\" newline

unquoted_argument : unquoted_element+ // TODO: unsupported_legacy
unquoted_element  : /[^\s\(\)#\"\\]/
                  | escape_sequence

escape_sequence  : escape_identity | escape_encoded | ESCAPE_SEMICOLON
escape_identity  : BACKSLASH /[^A-Za-z0-9;]/
escape_encoded   : ESCAPE_HORIZONTAL_TAB
                 | ESCAPE_CARRIAGE_RETURN
                 | ESCAPE_NEWLINE
ESCAPE_SEMICOLON : "\\;"

bracket_comment : POUND_SIGN bracket_argument
line_comment    : POUND_SIGN /[^\n]+/

BACKSLASH              : "\\"
ESCAPE_CARRIAGE_RETURN : "\\r"
ESCAPE_HORIZONTAL_TAB  : "\\t"
ESCAPE_NEWLINE         : "\\n"
LEFT_PARENTHESIS       : "("
POUND_SIGN             : "#"
QUOTATION_MARK         : "\""
RIGHT_PARENTHESIS      : ")"


%import common.WS_INLINE
%import common.NEWLINE
